pluginManagement {
    plugins {
        id 'com.jfrog.bintray' version "${getProperty("plugin.bintray.version")}"
        id 'com.github.johnrengelman.shadow' version "${getProperty("plugin.shadow.version")}"
        id 'com.github.hierynomus.license' version "${getProperty("plugin.license.version")}"
        id 'org.owasp.dependencycheck' version "${getProperty("plugin.dependencycheck.version")}"
        id 'com.github.spotbugs' version "${getProperty("plugin.spotbugs.version")}"
        id 'de.thetaphi.forbiddenapis' version "${getProperty("plugin.forbiddenapis.version")}"
        id 'com.github.sgtsilvio.gradle.utf8' version "${getProperty("plugin.utf8.version")}"
        id 'com.github.sgtsilvio.gradle.metadata' version "${getProperty("plugin.metadata.version")}"
        id 'com.github.sgtsilvio.gradle.javadoc-links' version "${getProperty("plugin.javadoc-links.version")}"
    }
}

rootProject.name = 'hivemq-community-edition'

if (file('../hivemq-extension-sdk').exists()) {
    if (gradle.parent == null) { // not part of a composite build
        includeBuild '../hivemq-extension-sdk'
    }
} else {
    logger.warn('''
######################################################################################################
You can not use the latest changes of or modify the hivemq-extension-sdk.
Please checkout the hivemq-extension-sdk repository next to the hivemq-community-edition repository.
Execute the following command from your project directory:
git clone https://github.com/hivemq/hivemq-extension-sdk.git ../hivemq-extension-sdk
You can also clone your fork:
git clone https://github.com/<replace-with-your-fork>/hivemq-extension-sdk.git ../hivemq-extension-sdk
######################################################################################################
''')
}
