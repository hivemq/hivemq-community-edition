plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow'
    id 'com.github.hierynomus.license'
    id 'org.owasp.dependencycheck'
    id 'idea'
    id 'com.github.sgtsilvio.gradle.metadata'

    /* Code Quality Plugins */
    id 'jacoco'
    id 'pmd'
    id 'com.github.spotbugs'
    id 'de.thetaphi.forbiddenapis'
}


/* ******************** metadata ******************** */

group 'com.hivemq'
description = 'HiveMQ CE is a Java-based open source MQTT broker that fully supports MQTT 3.x and MQTT 5'

ext.mainClassName = 'com.hivemq.HiveMQServer'

metadata {
    readableName = "HiveMQ Community Edition"
    organization {
        name = "dc-square GmbH"
        url = "https://www.hivemq.com/"
    }
    license {
        apache2()
    }
    developers {
        developer {
            it.id = "cschaebe"
            it.name = "Christoph Schaebel"
            it.email = "christoph.schaebel@hivemq.com"
        }
        developer {
            it.id = "lbrandl"
            it.name = "Lukas Brandl"
            it.email = "lukas.brandl@hivemq.com"
        }
        developer {
            it.id = "flimpoeck"
            it.name = "Florian Limpoeck"
            it.email = "florian.limpoeck@hivemq.com"
        }
        developer {
            it.id = "sauroter"
            it.name = "Georg Held"
            it.email = "georg.held@hivemq.com"
        }
        developer {
            it.id = "SgtSilvio"
            it.name = "Silvio Giebl"
            it.email = "silvio.giebl@hivemq.com"
        }
    }
    github {
        org = "hivemq"
        repo = "hivemq-community-edition"
        issues()
    }
}


/* ******************** java ******************** */

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}


/* ******************** dependencies ******************** */

repositories {
    mavenCentral()
}

dependencies {

    implementation "com.hivemq:hivemq-extension-sdk:${property('hivemq-extension-sdk.version')}"

    /* netty */
    implementation(group: 'io.netty', name: 'netty-handler', version: property('netty.version'))
    implementation(group: 'io.netty', name: 'netty-codec-http', version: property('netty.version'))

    /* logging */
    implementation(group: 'org.slf4j', name: 'jul-to-slf4j', version: property('slf4j.version'))
    implementation([group: 'ch.qos.logback', name: 'logback-classic', version: property('logback.version')]) {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    /* bouncycastles */
    implementation(group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: property('bouncycastles.version'))
    implementation(group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: property('bouncycastles.version'))

    /* rocksdb */
    implementation(group: 'org.rocksdb', name: 'rocksdbjni', version: property('rocksdb.version'))

    /* apache commons */
    implementation(group: 'commons-io', name: 'commons-io', version: property('commons-io.version'))
    implementation(group: 'commons-codec', name: 'commons-codec', version: property('commons-codec.version'))
    implementation(group: 'commons-collections', name: 'commons-collections', version: property('commons-collections.version'))
    implementation(group: 'org.apache.commons', name: 'commons-lang3', version: property('commons-lang.version'))

    /* xml */
    implementation(group: 'com.sun.xml.bind', name: 'jaxb-core', version: property('jaxb-core.version'))
    implementation(group: 'com.sun.xml.bind', name: 'jaxb-impl', version: property('jaxb-impl.version'))
    implementation(group: 'javax.xml.bind', name: 'jaxb-api', version: property('jaxb-api.version'))

    /* javax */
    implementation(group: 'javax.activation', name: 'activation', version: property('javax.activation.version'))
    implementation(group: 'javax.validation', name: 'validation-api', version: property('javax.validation.version'))
    implementation(group: 'javax.annotation', name: 'javax.annotation-api', version: property('javax.annotation.version'))

    /* hashing */
    implementation(group: 'net.openhft', name: 'zero-allocation-hashing', version: property('zero-allocation-hashing.version'))

    /* temporary dependencies to override transitive ones that have security vulnerabilities*/
    implementation(group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '1.3.71')
    implementation(group: 'org.apache.commons', name: 'commons-compress', version: '1.20')

    /* guava */
    implementation([group: 'com.google.guava', name: 'guava', version: property('guava.version')]) {
        //We don't need the HTTP Client since we are only interested in the server version
        exclude group: 'org.checkerframework', module: 'checker-qual'
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
    }

    /* oshi */
    implementation([group: 'com.github.oshi', name: 'oshi-core', version: property('oshi.version')]) {
        //We don't need the HTTP Client since we are only interested in the server version
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    /* metrics */
    implementation([group: 'io.dropwizard.metrics', name: 'metrics-logback', version: property('metrics.version')]) {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
    implementation([group: 'io.dropwizard.metrics', name: 'metrics-core', version: property('metrics.version')]) {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    implementation([group: 'io.dropwizard.metrics', name: 'metrics-jmx', version: property('metrics.version')]) {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    /* xodus */
    implementation([group: 'org.jetbrains.xodus', name: 'xodus-environment', version: property('xodus.version')]) {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.jetbrains', module: 'annotations'
    }
    implementation([group: 'org.jetbrains.xodus', name: 'xodus-openAPI', version: property('xodus.version')]) {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.jetbrains', module: 'annotations'
    }
    implementation([group: 'org.jetbrains.xodus', name: 'xodus-entity-store', version: property('xodus.version')]) {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.jetbrains', module: 'annotations'
    }
    implementation([group: 'org.jetbrains.xodus', name: 'xodus-utils', version: property('xodus.version')]) {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.jetbrains', module: 'annotations'
    }

    /* jackson */
    implementation(group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: property('jackson.version'))
    implementation(group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: property('jackson.version'))
    implementation([group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-guava', version: property('jackson.version')]) {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
    }
    implementation([group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: property('jackson.version')]) {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
    }

    /* dependency injection */
    implementation([group: 'com.google.inject', name: 'guice', version: property('guice.version')]) {
        exclude group: 'com.google.guava', module: 'guava'
    }

    testImplementation(group: 'junit', name: 'junit', version: property('junit.version'))
    testImplementation(group: 'org.mockito', name: 'mockito-core', version: property('mockito.version'))
    testImplementation(group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: property('equalsverifier.version'))
    testImplementation(group: 'net.jodah', name: 'concurrentunit', version: property('concurrentunit.version'))
    testImplementation(group: 'org.jboss.shrinkwrap', name: 'shrinkwrap-api', version: property('shrinkwrap.version'))
    testImplementation(group: 'net.bytebuddy', name: 'byte-buddy', version: property('bytebuddy.version'))
    testImplementation(group: 'com.github.tomakehurst', name: 'wiremock-standalone', version: property('wiremock.version'))
    testImplementation([group: 'com.github.stefanbirkner', name: 'system-rules', version: property('system-rules.version')]) {
        exclude group: 'junit', module: 'junit-dep'
    }

    /* We need some HotSpot methods to generate heapdumps from code */
    testImplementation files("${System.getProperty('java.home')}/../lib/sa-jdi.jar")

    testRuntimeOnly(group: 'org.jboss.shrinkwrap', name: 'shrinkwrap-impl-base', version: property('shrinkwrap.version'))

    //plugins for spotbugs
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.8.0'
}


/* ******************** test ******************** */

tasks.test {
    jvmArgs += ["-Dfile.encoding=UTF-8", "-noverify", "--add-opens", "java.base/java.lang=ALL-UNNAMED", "--add-opens", "java.base/java.nio=ALL-UNNAMED", "--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED", "--add-opens", "jdk.management/com.sun.management.internal=ALL-UNNAMED", "--add-exports", "java.base/jdk.internal.misc=ALL-UNNAMED"]
    minHeapSize = "128m"
    maxHeapSize = "2048m"

    /* Use inclusions, exclusions from file so we can run in parallel */
    File exclusions = new File("exclusions.txt")
    if (exclusions.exists()) {
        def lines = exclusions.readLines()
        exclude lines
        logger.lifecycle("Excluded " + lines.size() + " tests for this execution")
    }

    testLogging.events("failed")
    testLogging.exceptionFormat("full")

    /* use tmpdir from gradle property or the same tmpdir as the runner */
    if (project.hasProperty("test_temp_dir")) {
        jvmArgs "-Djava.io.tmpdir=" + test_temp_dir
    } else {
        def tempDir = System.properties['java.io.tmpdir']
        if (tempDir) {
            jvmArgs "-Djava.io.tmpdir=" + tempDir
        }
    }
}


/* ******************** packaging ******************** */

ext {
    projectString = "hivemq-ce-${project.version}"
    packagingDir = "${buildDir}/package"
    packagingProjectDir = "${packagingDir}/${projectString}"
    zipDir = "${buildDir}/zip"
}

tasks.jar {
    manifest {
        attributes 'Implementation-Title': 'HiveMQ'
        attributes 'Implementation-Vendor': metadata.organization.name
        attributes 'Implementation-Version': project.version
        attributes 'HiveMQ-Version': project.version
        attributes 'Main-Class': project.mainClassName
    }
}

tasks.shadowJar {
    mergeServiceFiles()
}

tasks.register('cleanPackaging') {
    group 'packaging'

    doFirst {
        delete "${project.packagingDir}"
    }
}

tasks.register('copyXml', Copy) {
    group 'packaging'

    mustRunAfter tasks.cleanPackaging

    from "${projectDir}/src/main/resources/config.xml"
    into "${project.packagingProjectDir}/conf"
}

tasks.register('copyPackaging', Copy) {
    group 'packaging'

    mustRunAfter tasks.cleanPackaging

    from "${projectDir}/src/packaging"
    into "${project.packagingProjectDir}"

    doLast {
        new File("${project.packagingProjectDir}/data").mkdirs()
        new File("${project.packagingProjectDir}/log").mkdirs()
    }
}

tasks.register('copyJar', Copy) {
    group 'packaging'

    mustRunAfter tasks.cleanPackaging
    dependsOn tasks.shadowJar

    from tasks.shadowJar
    into "${project.packagingProjectDir}/bin"

    rename { String fileName ->
        fileName.replace(tasks.shadowJar.archiveFileName.get(), "hivemq.jar")
    }
}

tasks.register('packaging', Zip) {
    group 'packaging'

    dependsOn tasks.cleanPackaging
    dependsOn tasks.copyXml
    dependsOn tasks.copyPackaging
    dependsOn tasks.copyJar

    from "${project.packagingDir}"
    destinationDir new File(project.zipDir)
    archiveName "${projectString}.zip"
    include '**'
}

defaultTasks 'clean', 'packaging'

tasks.javadoc {
    options.charSet = 'UTF-8'
    options.encoding = 'UTF-8'

    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('-html5')
    options.addBooleanOption '-no-module-directories', true

    doLast {
        javaexec {
            main = '-jar'
            args = ["${projectDir}/gradle/tools/javadoc-cleaner-1.0.jar"]
        }
    }
}


/* ******************** checks ******************** */

ext {
    jacocoVersion = '0.8.5'
    pmdVersion = '6.22.0'
    spotbugsVersion = '3.1.12'
}

if (!System.properties['java.version'].startsWith('11')) {
    throw new RuntimeException('Incompatible JRE version: ' + System.properties['java.version'] + '. Use JRE 11 instead.')
}

jacoco {
    toolVersion = jacocoVersion
}

pmd {
    toolVersion = pmdVersion
    sourceSets = [sourceSets.main]
    ignoreFailures = true
    rulePriority = 3
}

spotbugs {
    toolVersion = spotbugsVersion
    sourceSets = [sourceSets.main]
    ignoreFailures = true
    reportsDir = file("${buildDir}/reports/findbugs")
    reportLevel = 'medium'
}

dependencyCheck {
    analyzers {
        centralEnabled = false
    }
    format = 'ALL'
    scanConfigurations = ['compile']
    suppressionFile = "${projectDir}/gradle/dependency-check/suppress.xml"
    failOnError = false
}
check.dependsOn dependencyCheckAnalyze

forbiddenApis {
    bundledSignatures = ['jdk-system-out']
    ignoreFailures = false
}

forbiddenApisMain {
    exclude '**/BatchedException.class'
    exclude '**/LoggingBootstrap.class'
}

forbiddenApisTest.enabled = false


/* ******************** compliance ******************** */

license {
    header file("${rootDir}/HEADER")
    mapping('java', 'SLASHSTAR_STYLE')
}

downloadLicenses {
    ext.apacheTwo = license('Apache License, Version 2.0', 'http://opensource.org/licenses/Apache-2.0')
    ext.cddlOnePointZero = license('CDDL, Version 1.0', 'http://glassfish.java.net/public/CDDLv1.0.html')
    ext.cddlOnePointOne = license('CDDL, Version 1.1', 'http://glassfish.java.net/public/CDDL+GPL_1_1.html')
    ext.lgplTwoPointOne = license('LGPL, Version 2.1', 'http://www.gnu.org/licenses/lgpl-2.1.html')
    ext.eplOnePointZero = license('EPL, Version 1.0', 'http://www.eclipse.org/legal/epl-v10.html')

    aliases = [
            (apacheTwo)       : ['Apache License 2', 'Apache Software License - Version 2.0', 'The Apache Software License, Version 2.0', 'Apache 2', 'Apache License Version 2.0', 'Apache License, Version 2.0', 'Apache License 2.0', 'Apache 2.0', 'The Apache License, Version 2.0', license('Apache License', 'http://www.apache.org/licenses/LICENSE-2.0')],
            (cddlOnePointZero): ['Common Development and Distribution License (CDDL) v1.0', 'COMMON DEVELOPMENT AND DISTRIBUTION LICENSE (CDDL) Version 1.0'],
            (cddlOnePointOne) : ['CDDL 1.1'],
            (lgplTwoPointOne) : ['LGPL 2.1', 'GNU Lesser General Public License'],
            (eplOnePointZero) : ['Eclipse Public License - Version 1.0', 'Eclipse Public License', 'Eclipse Public License - v 1.0']
    ]
}

tasks.register('updateThirdPartyLicenses') {
    group 'license'
    dependsOn tasks.downloadLicenses
    doLast {
        javaexec {
            main = '-jar'
            args = ["${projectDir}/gradle/tools/license-third-party-tool-1.0.jar",
                    "${buildDir}/reports/license/dependency-license.xml",
                    "${projectDir}/src/packaging/third-party-licenses/licenses",
                    "${projectDir}/src/packaging/third-party-licenses/licenses.html"
            ]
        }
    }
}


/* ******************** publishing ******************** */

publishing {
    publications {
        distribution(MavenPublication) {
            artifact tasks.packaging

            artifactId 'hivemq-community-edition'
        }

        embedded(MavenPublication) {
            from components.java

            artifactId 'hivemq-community-edition-embedded'
            }
        }
    }


/* ******************** ide ******************** */

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
